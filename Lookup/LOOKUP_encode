### vim:ft=zsh:foldmethod=marker
## url encoding for lookup queries, etc.

LOOKUP_guard || return 1
emulate -L zsh
setopt extendedglob
local input output
local -A opts

zparseopts -A opts -D s q || return 1
for opt in ${(k)opts} ; do
    [[ -z ${opts[$opt]} ]] && opts[$opt]='yes'
done

if [[ ${opts[-q]} == 'yes' ]] ; then
    # -q: QUERY is declared 'local -x QUERY' in backends.
    #     That means, it gets handed down to us. Apply our filters to it.
    unencQUERY="${QUERY}"
    set -- "${QUERY}"
fi

if [[ ${opts[-s]} == 'yes' ]] ; then
    local ws=$'\t '
    input=${${1##[$ws]#}%%[$ws]#}
    input=${input//[$ws]##/+}
else
    input="$1"
fi

input=( ${(s::)input} )
output=${(j::)input/(#b)([^A-Za-z0-9_.!~*\'\(\)+-])/%${(l:2::0:)$(([##16]#match))}}
if [[ ${opts[-q]} == 'yes' ]] ; then
    QUERY=${output}
else
    printf '%s' ${output}
fi
