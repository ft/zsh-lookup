### vim:ft=zsh:foldmethod=marker
## run hooks
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

local hook obackend olsys ret
local -a hooks

obackend="${backend}"
if [[ $1 != '--' ]] ; then
    backend="$1"
fi
shift

zstyle -t ":lookup:${lsys}:${backend}" debug &&
    printf 'LOOKUP_hook: current context: "%s"\n' ":lookup:${lsys}:${backend}"

zstyle -a ":lookup:${lsys}:${backend}" hooks hooks || return 0

olsys=${lsys}
lsys='-hook-'
for hook in ${hooks} ; do
    (( ${+functions[$hook]} == 0 )) && continue
    ${hook}
    case $? in
        (1)
            break
            ;;
        (0|*)
            ;;
    esac
done
lsys="${olsys}"
backend="${obackend}"
return 0
