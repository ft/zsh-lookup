### vim:ft=zsh:foldmethod=marker
##
## Spawn an appropriate browser.
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>
##

local arg debug format lookup_browser uri i
local -a lookup_browser_args

lsys='-browser-'
uri="$1"

LOOKUP_hook "$@"
zstyle -t ":lookup:${lsys}:${backend}" debug &&
    printf 'LOOKUP_browser: current context: "%s"\n' ":lookup:${lsys}:${backend}"

if (( lookup_printout > 0 )) || zstyle -t ":lookup:${lsys}:${backend}" debug ; then
    debug='yes'
fi

if (( lookup_remote > 0 )) ; then
    zstyle -s ":lookup:${lsys}:${backend}" gui_browser lookup_browser
    zstyle -a ":lookup:${lsys}:${backend}" gui_formats lookup_browser_args
else
    zstyle -s ":lookup:${lsys}:${backend}" txt_browser lookup_browser
    zstyle -a ":lookup:${lsys}:${backend}" txt_formats lookup_browser_args
fi

[[ -z ${lookup_browser} ]] && lookup_browser=${BROWSER:-w3m}
(( ${#lookup_browser_args} == 0 )) && lookup_browser_args=( "%s" )

for i in {1..${#lookup_browser_args}} ; do
    zformat -f format ${lookup_browser_args[$i]} s:${uri}
    lookup_browser_args[$i]="${format}"
done

if [[ -n ${debug} ]] ; then
    print -- ${lookup_browser} ${(qq)lookup_browser_args}
    return 0
else
    ${lookup_browser} ${lookup_browser_args}
    return $?
fi
