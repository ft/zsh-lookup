### vim:ft=zsh:foldmethod=marker
## usenet search backend for lookup via groups.google.com
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

LOOKUP_guard || return 1
[[ -n ${lookup_describe} ]] &&
    printf '%s' 'usenet search' &&
    return 0

local -a comp_args
local -A known_modes

known_modes=(
    text    'Free text search'
    msgid   'Search for Message-ID: header'
    author  'Search for a specific author'
)

(( ${+functions[LOOKUP_help_$backend]} )) ||
function LOOKUP_help_${backend}() {
    printf 'usage: %s [-{t,m}] [-a <arg>] <query>\n' ${backend}
    printf '  -m            search for message id\n'
    printf '  -t            free text search\n'
    printf '  -a <author>   search for messages by <author>\n'
}
LOOKUP_help ${backend} && return 0

if [[ -n ${lookup_complete} ]] ; then
    comp_args=(
        '-a[specify author]:author:'
        '-m[search for message id]:message id:'
        '-t[free text search]:text:'
        '*:usenet search string:true'
    )

    _arguments -s -w -A '-*' ${comp_args} && return 0
    _message 'usenet search string'
    return 0
fi

local mode author
local -A opts
local -x QUERY

mode='text'
zparseopts -A opts -D m a: t || return 1
for opt in ${(k)opts} ; do
    [[ -z ${opts[$opt]} ]] && opts[$opt]='yes'
done
[[ ${opts[-t]} == 'yes' ]] && mode='text'
[[ ${opts[-m]} == 'yes' ]] && mode='msgid'
[[ -n ${opts[-a]} ]] && mode='author' && author="&as_uauthors=${opts[-a]}"

QUERY="$*"
LOOKUP_query_handler
if [[ -z ${(M)known_modes:#$mode} ]] ; then
    printf 'Unknown mode: '\''%s'\''.\n\n' ${mode}
    QUERY=''
fi
if [[ -z ${QUERY} ]] && [[ ${mode} != 'author' ]] ; then
    LOOKUP_help_${backend}
    return 1
fi

LOOKUP_encode -s -q
if [[ ${mode} == 'msgid' ]] ; then
    LOOKUP_browser "http://groups.google.com/groups?selm=${QUERY}"
    return $?
fi

LOOKUP_browser "http://groups.google.com/groups?q=${QUERY}${author}"
return $?
