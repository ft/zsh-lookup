### vim:ft=zsh:foldmethod=marker
## google search backend for lookup
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

LOOKUP_guard || return 1
[[ -n ${lookup_describe} ]] && printf '%s' 'google websearch' && return 0

if [[ -n ${lookup_complete} ]] ; then
    local -a comp_args
    comp_args=(
        '-1[I'\''m Feeling Lucky!]'
        '*:dict.leo.org query:true'
    )

    _arguments -s -w -A '-*' ${comp_args} && return 0
    _message 'google query'
    return 0
fi

local mode
local -x QUERY

(( ${+functions[LOOKUP_help_$backend]} )) ||
function LOOKUP_help_${backend}() {
    printf 'usage: %s [-1] <query>\n' ${backend}
    printf '  -1    Go directly to the first hit using "I'\''m Feeling Lucky"\n'
    printf '\n Make web searches via google.com\n'
    printf ' If you always want to use the -1 option, you may set the use-first-hit\n'
    printf ' style in this context: :lookup:%s:%s\n' ${lsys} ${backend}
    printf '\nExamples:\n'
    printf ' %% lookup %s zsh\n' ${backend}
    printf ' %% lookup %s -1 openbsd\n' ${backend}
    printf ' %% zstyle '\'':lookup:*:%s'\'' use-first-hit true\n\n' ${backend}
}
LOOKUP_help ${backend} && return 0

zparseopts -A opts -D 1 || return 1
for opt in ${(k)opts} ; do
    [[ -z ${opts[$opt]} ]] && opts[$opt]='yes'
done

if [[ ${opts[-1]} == 'yes' ]] ||
   zstyle -t ":lookup:${lsys}:${backend}" use-first-hit ; then

    mode='I'\''m Feeling Lucky'
    mode="$(LOOKUP_encode ${mode})"
    mode="&btnI=${mode}"
else
    mode=''
fi

QUERY="$*"
LOOKUP_query_handler
if [[ -z ${QUERY} ]] ; then
    LOOKUP_help_${backend}
    return 1
fi

LOOKUP_encode -s -q
LOOKUP_browser "http://www.google.com/search?q=${QUERY}${mode}"
return $?
