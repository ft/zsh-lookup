### vim:ft=zsh:foldmethod=marker
## lists.debian.org message-id search backend for lookup
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

LOOKUP_guard || return 1
[[ -n ${lookup_describe} ]] &&
    printf '%s' 'search lists.debian.org by message-ids' &&
    return 0

local -a comp_args

(( ${+functions[LOOKUP_help_$backend]} )) ||
function LOOKUP_help_${backend}() {
    printf 'usage: %s [-1] <query>\n' ${backend}
    printf '  -1    use the search'\''s first-hit option\n'
}
LOOKUP_help ${backend} && return 0

if [[ -n ${lookup_complete} ]] ; then
    comp_args=(
        '-1[use /firsthit option]'
        '*:message-id for l.d.o:true'
    )

    _arguments -s -w -A '-*' ${comp_args} && return 0
    _message 'message-id for l.d.o'
    return 0
fi

local end
local -A opts
local -x QUERY

zparseopts -A opts -D '1'
for opt in ${(k)opts} ; do
    [[ -z ${opts[$opt]} ]] && opts[$opt]='yes'
done
[[ ${opts[-1]} == 'yes' ]] && end='/firsthit' || end='/links'

QUERY="$*"
LOOKUP_query_handler
if [[ -z ${QUERY} ]] ; then
    LOOKUP_help_${backend}
    return 1
fi

LOOKUP_encode -q
LOOKUP_browser "http://lists.debian.org/msgid-search/${QUERY}${end}"
return $?
