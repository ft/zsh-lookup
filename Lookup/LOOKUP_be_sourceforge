### vim:ft=zsh:foldmethod=marker
## sf.net search backend for lookup
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

[[ -n ${lookup_describe} ]] &&
    printf '%s' 'search sourceforge.net' &&
    return 0

local -a comp_args

(( ${+functions[LOOKUP_help_$backend]} )) ||
function LOOKUP_help_${backend}() {
    printf 'usage: %s <query>\n' ${backend}
}
LOOKUP_help ${backend} && return 0

if [[ -n ${lookup_complete} ]] ; then
    _message 'sf.net search string'
    return 0
fi

local -x QUERY

QUERY="$*"
LOOKUP_query_handler
if [[ -z ${QUERY} ]] ; then
    LOOKUP_help_${backend}
    return 1
fi

LOOKUP_encode -s -q
LOOKUP_browser "http://sourceforge.net/search/?words=${QUERY}&type_of_search=soft"
return $?
