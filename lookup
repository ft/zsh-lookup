### vim:ft=zsh:foldmethod=marker
##
## zsh function that looks up an argument in various webservices.
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>
##
## This file, LOOKUP_browser and all backends in the Lookup/ subdirectory
## are distributed under the same licence as zsh itself (BSD-like).
##

# TODO: LOOKUP_encode() should be used throughout the system.
# TODO: Backends should be selfdocumenting.
# TODO: completions should be able to hide backends that got an alias, too.

typeset -ga LOOKUP_backends
typeset -gA LOOKUP_aliases

function lookup() {
    emulate -L zsh
    setopt extendedglob

    local file opt
    local -a opts
    local -A lu_aliases

    local -x backend lookup_describe
    local -ix lookup_remote lookup_printout

    if [[ -z $1 ]] ; then
        printf 'usage: lookup [-{i,a,d,l,L,P,R}] <backend> OPTION(s)...\n'
        printf '    -i  (re)initialize lookup\n\n'
        printf '    -a  add a backend alias\n'
        printf '    -d  remove an alias for a backend\n\n'
        printf '    -l  list available backends\n'
        printf '    -L  list defined backend aliases\n\n'
        printf '    -P  print which browser command would be used\n'
        printf '    -R  send url to remote browser\n'
        return 0
    fi

    lookup_remote=0
    lookup_printout=0
    zparseopts -A opts -D a: d: i l L P R
    if [[ $1 == -* ]] ; then
        printf 'Unknown option: %s\n' "$1"
        return 1
    fi
    for opt in ${(k)opts} ; do
        [[ -z ${opts[$opt]} ]] && opts[$opt]='yes'
    done

    if [[ -n ${opts[-l]} ]] ; then
        printf 'Available backends:\n\n'
        lookup_describe="yes"
        for backend in ${LOOKUP_backends} ; do
            printf '%16s - %s\n' ${backend} "$(LOOKUP_be_${backend})"
        done
        printf '\n'
        return 0
    fi

    if [[ -n ${opts[-L]} ]] ; then
        if (( ${#LOOKUP_aliases} == 0 )) ; then
            printf 'lookup: No aliases defined.\n'
            return 0
        fi
        printf 'Defined backend aliases:\n\n'
        for al in ${(k)LOOKUP_aliases}; do
            printf '%16s=%s\n' ${al} ${LOOKUP_aliases[$al]}
        done
        printf '\n'
        return 0
    fi

    if [[ -n ${opts[-i]} ]] ; then
        local f
        local -a fcts

        LOOKUP_backends=()
        fcts=(LOOKUP_browser)

        for file in ${^fpath}/LOOKUP_be_*~*(\~|.zwc)(N.) ; do
            file=${file:t}
            : ${file:#(#b)LOOKUP_be_(*)}
            backend=${match[1]}

            [[ -n ${(M)LOOKUP_backends:#${backend}} ]] && continue
            LOOKUP_backends+=(${backend})
            autoload -Uz LOOKUP_be_${backend}
        done

        for f in ${fcts} ; do
            (( ${+functions[$f]} )) || autoload -Uz $f
        done
        return 0
    fi

    if [[ -n ${opts[-a]} ]] ; then
        local al val

        : ${${opts[-a]}/(#b)(*)=(*)}
        al="${match[1]}"
        val="${match[2]}"

        LOOKUP_aliases[$al]="${val}"
        return 0
    fi

    if [[ -n ${opts[-d]} ]] ; then
        unset "LOOKUP_aliases[${opts[-d]}]"
        return 0
    fi

    [[ -n ${opts[-R]} ]] && lookup_remote=1
    [[ -n ${opts[-P]} ]] && lookup_printout=1

    backend=${${LOOKUP_aliases[$1]}:-$1}

    if [[ -z ${(M)LOOKUP_backends:#$backend} ]] ; then
        printf 'Unknown backend '\''%s'\''.\n' ${backend}
        return 1
    fi

    shift

    LOOKUP_be_${backend} "$@"
    return $?
}

function lu() {
    lookup "$@"
}

LOOKUP_encode() {
    emulate -L zsh
    setopt extendedglob
    local input

    input=( ${(s::)1} )
    printf '%s' ${(j::)input/(#b)([^A-Za-z0-9_.!~*\'\(\)+-])/%${(l:2::0:)$(([##16]#match))}}
}

# initialize the system
lookup -i
